#:include 'common.fypp'
module test_paramcard_77
    use testdrive, only: new_unittest, unittest_type, error_type, check
    use test_util
    use paramcard, only: parse_param
    implicit none
    private

    public :: collect_test_paramcard_77
contains

    subroutine collect_test_paramcard_77(testsuite)
        type(unittest_type), allocatable, intent(out) :: testsuite(:)

        testsuite = [ &
#:for k in ['s', 'i', 'r', 'd']
                    new_unittest('paramcard_get_${k}$', test_paramcard_get_${k}$), &
#:endfor
#:for k in ['s', 'i', 'r']
                    new_unittest('paramcard_set_${k}$', test_paramcard_set_${k}$), &
#:endfor
                    new_unittest('paramcard_set_s', test_paramcard_set_d) &
                    ]
    end subroutine collect_test_paramcard_77

    subroutine test_paramcard_get_s(error)
        type(error_type), allocatable, intent(out) :: error

        character(len=5) :: s

        ! Value being longer than the length -> truncated.
        call parse_param('paramcard command: clear')
        call paramcard_get_s('a', s, 'aB 123')
        call check(error, s, 'aB 12')
        if (allocated(error)) return
        call check(error, param_changed())  ! truncated
        if (allocated(error)) return

        ! Value being shorter than the length.
        call parse_param('paramcard command: clear')
        call paramcard_get_s('a', s, 'a1')
        call check(error, s, 'a1   ')
        if (allocated(error)) return
        call check(error,.not. param_changed())
        if (allocated(error)) return

        ! Same variable for `variable` and `default_value`, unchanged.
        call parse_param('paramcard command: clear')
        s = 'a1234'
        call paramcard_get_s('a', s, s)
        call check(error, s, 'a1234')
        if (allocated(error)) return
        call check(error,.not. param_changed())
        if (allocated(error)) return

        ! Same variable for `variable` and `default_value`, changed.
        call parse_param('paramcard command: clear')
        call parse_param('a = b1234')
        s = 'a1234'
        call paramcard_get_s('a', s, s)
        call check(error, s, 'b1234')
        if (allocated(error)) return
        call check(error, param_changed())
        if (allocated(error)) return
    end subroutine test_paramcard_get_s

#:for t, k in [('integer', 'i'), ('real', 'r'), ('double precision', 'd')]
    subroutine test_paramcard_get_${k}$ (error)
        type(error_type), allocatable, intent(out) :: error

        ${t}$::x,y

        call parse_param('paramcard command: clear')
        x = 456
        call paramcard_get_${k}$ ('x', x, x)
        y = 456
        call check(error, x, y)
        if (allocated(error)) return
        call check(error,.not. param_changed())
        if (allocated(error)) return

        call parse_param('paramcard command: clear')
        call parse_param('x = 123')
        x = 456
        call paramcard_get_${k}$ ('x', x, x)
        y = 123
        call check(error, x, y)
        if (allocated(error)) return
        call check(error, param_changed())
        if (allocated(error)) return
    end subroutine test_paramcard_get_${k}$

#:endfor
    subroutine test_paramcard_set_s(error)
        type(error_type), allocatable, intent(out) :: error

        character(len=5) :: a, b, c

        call parse_param('paramcard command: clear')
        a = 'abc42'
        b = a
        c = 'fail'
        call paramcard_set_s('a', a)
        call paramcard_get_s('a', a, c)
        call check(error, a, b)
        if (allocated(error)) return
        call check(error, param_changed())
        if (allocated(error)) return
    end subroutine test_paramcard_set_s

#:for t, k in [('integer', 'i'), ('real', 'r'), ('double precision', 'd')]
    subroutine test_paramcard_set_${k}$ (error)
        type(error_type), allocatable, intent(out) :: error

        ${t}$::a,b,c

        call parse_param('paramcard command: clear')
        a = 42
        b = a
        c = 0
        call paramcard_set_${k}$ ('a', a)
        call paramcard_get_${k}$ ('a', a, c)
        call check(error, a, b)
        if (allocated(error)) return
        call check(error, param_changed())
        if (allocated(error)) return
    end subroutine test_paramcard_set_${k}$

#:endfor
end module test_paramcard_77

program tester
    use, intrinsic :: iso_fortran_env, only: error_unit
    use testdrive, only: run_testsuite, new_testsuite, testsuite_type
    use test_paramcard_77, only: collect_test_paramcard_77
    implicit none
    integer :: stat, is
    type(testsuite_type), allocatable :: testsuites(:)
    character(len=*), parameter :: fmt = '("#", *(1x, a))'

    stat = 0

    testsuites = [ &
                 new_testsuite('paramcard_77', collect_test_paramcard_77) &
                 ]

    do is = 1, size(testsuites)
        write (error_unit, fmt) 'Testing:', testsuites(is)%name
        call run_testsuite(testsuites(is)%collect, error_unit, stat)
    end do

    if (stat > 0) then
        write (error_unit, '(i0, 1x, a)') stat, 'test(s) failed!'
        error stop
    end if
end program tester
